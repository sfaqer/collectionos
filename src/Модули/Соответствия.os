// Преобразует переданное соответствие в карту
//
// Параметры:
//   Соответствие - Соответствие - Исходное соответствие для преобразования
//
// Возвращаемое значение:
//   Карта - Карта, содержащая элементы исходного соответствия
//
Функция КакКарта(Соответствие) Экспорт

	Результат = Новый КартаСоответствие();

	Рефлектор = Новый Рефлектор;

	Рефлектор.УстановитьСвойство(
		Результат,
		"Соответствие",
		Скопировать(Соответствие)
	);

	Возврат Результат;

КонецФункции

// Создает соответствие из переданных элементов
//
// Параметры:
//   Ключ1     - Произвольный - Первый ключ для создания соответствия
//   Значение1 - Произвольный - Значение для первого ключа
//   Ключ2     - Произвольный - Второй ключ для создания соответствия
//   Значение2 - Произвольный - Значение для второго ключа
//   Ключ3     - Произвольный - Третий ключ для создания соответствия
//   Значение3 - Произвольный - Значение для третьего ключа
//   Ключ4     - Произвольный - Четвертый ключ для создания соответствия
//   Значение4 - Произвольный - Значение для четвертого ключа
//   Ключ5     - Произвольный - Пятый ключ для создания соответствия
//   Значение5 - Произвольный - Значение для пятого ключа
//   Ключ6     - Произвольный - Шестой ключ для создания соответствия
//   Значение6 - Произвольный - Значение для шестого ключа
//   Ключ7     - Произвольный - Седьмой ключ для создания соответствия
//   Значение7 - Произвольный - Значение для седьмого ключа
//   Ключ8     - Произвольный - Восьмой ключ для создания соответствия
//   Значение8 - Произвольный - Значение для восьмого ключа
//   Ключ9     - Произвольный - Девятый ключ для создания соответствия
//   Значение9 - Произвольный - Значение для девятого ключа
//   Ключ10     - Произвольный - Десятый ключ для создания соответствия
//   Значение10 - Произвольный - Значение для десятого ключа
//
// Возвращаемое значение:
//   ФиксированноеСоответствие - Соответствие, сформированное из переданных элементов
//
Функция ИзЭлементов(
	// BSLLS:NumberOfParams-off
	// BSLLS:NumberOfOptionalParams-off
	// BSLLS:UnusedParameters-off
		Ключ1 = NULL, Значение1 = NULL, Ключ2 = NULL, Значение2 = NULL,
		Ключ3 = NULL, Значение3 = NULL, Ключ4 = NULL, Значение4 = NULL,
		Ключ5 = NULL, Значение5 = NULL, Ключ6 = NULL, Значение6 = NULL,
		Ключ7 = NULL, Значение7 = NULL, Ключ8 = NULL, Значение8 = NULL,
		Ключ9 = NULL, Значение9 = NULL, Ключ10 = NULL, Значение10 = NULL
	// BSLLS:NumberOfParams-on
	// BSLLS:NumberOfOptionalParams-on
	// BSLLS:UnusedParameters-on
	) Экспорт

	Результат = Новый Соответствие;

	Для сч = 1 По 10 Цикл

		Ключ     = Вычислить("Ключ" + сч);
		Значение = Вычислить("Значение" + сч);

		Если Ключ = NULL Или Значение = NULL Тогда
			Прервать;
		КонецЕсли;

		Результат.Вставить(Ключ, Значение);

	КонецЦикла;

	Возврат Новый ФиксированноеСоответствие(Результат);

КонецФункции

// Создает копию переданного соответствия
//
// Параметры:
//   Соответствие - Соответствие - Исходное соответствие для копирования
//                - ФиксированноеСоответствие - Исходное соответствие для копирования
//
// Возвращаемое значение:
//   Соответствие - Новое соответствие, содержащее те же элементы, что и исходное
//
Функция Скопировать(Соответствие) Экспорт

	Если ТипЗнч(Соответствие) = Тип("ФиксированноеСоответствие") Тогда
		Результат = Новый Соответствие(Соответствие);
	Иначе
		Результат = Новый Соответствие(Новый ФиксированноеСоответствие(Соответствие));
	КонецЕсли;

	Возврат Результат;

КонецФункции
