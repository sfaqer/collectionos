#Использовать asserts

#Использовать ".."
#Использовать "./ТестМодуль"

Перем Рефлектор;               // Рефлектор
Перем ДляКаждогоВыполнилсяРаз; // Количество раз которое выполнился метод ДляКаждого
Перем МножествоФикстура;       // Кеш множества фикстуры

&Тест
Процедура МножествоКартаСоздается() Экспорт
	
	// Дано

	// Когда

	Результат = Новый МножествоКарта;

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("МножествоКарта");

КонецПроцедуры

&Тест
Процедура Итератор() Экспорт

	// Дано
	
	МножествоКарта = Новый МножествоКарта;

	// Когда

	Результат = МножествоКарта.Итератор();

	// Тогда
	
	Ожидаем.Что(Результат).ИмеетТип("ИтераторКлючСоответствие");

КонецПроцедуры

&Тест
Процедура ДляКаждого() Экспорт

	// Дано

	ДляКаждогоВыполнилсяРаз = 0;

	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		Соответствия.КакКарта(Соответствие)
	);

	// Когда

	МножествоКарта.ДляКаждого(Новый Действие(ЭтотОбъект, "ДействиеДляКаждого"));

	// Тогда
	
	Ожидаем.Что(ДляКаждогоВыполнилсяРаз).Равно(3);

КонецПроцедуры

&Тест
Процедура ДляКаждогоЛямбда() Экспорт

	// Дано

	ТестМодуль.ДляКаждогоВыполнилсяРаз = 0;
	ТестМодуль.СуммаЭлементов          = 0;

	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		Соответствия.КакКарта(Соответствие)
	);

	// Когда

	МножествоКарта.ДляКаждого(
		"Элемент -> ТестМодуль.ДляКаждогоВыполнилсяРаз = ТестМодуль.ДляКаждогоВыполнилсяРаз + 1;
		|	ТестМодуль.СуммаЭлементов = ТестМодуль.СуммаЭлементов + Элемент;"
	);

	// Тогда
	
	Ожидаем.Что(ТестМодуль.ДляКаждогоВыполнилсяРаз).Равно(3);
	Ожидаем.Что(ТестМодуль.СуммаЭлементов).Равно(6);

КонецПроцедуры

&Тест
Процедура Содержит() Экспорт

	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		Соответствия.КакКарта(Соответствие)
	);

	// Когда

	Результат = МножествоКарта.Содержит(2);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура НеСодержит() Экспорт

	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		Соответствия.КакКарта(Соответствие)
	);

	// Когда

	Результат = МножествоКарта.Содержит(4);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура СодержитВсе() Экспорт

	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		Соответствия.КакКарта(Соответствие)
	);

	// Когда

	Результат = МножествоКарта.СодержитВсе(
		МножествоФикстура()
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура СодержитНеВсе() Экспорт

	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);

	МножествоКарта = Новый МножествоКарта;

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		Соответствия.КакКарта(Соответствие)
	);

	// Когда

	Результат = МножествоКарта.СодержитВсе(
		МножествоФикстура()
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура Пусто() Экспорт

	// Дано

	МножествоКарта = Новый МножествоКарта;

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		Соответствия.КакКарта(Новый Соответствие)
	);

	// Когда

	Результат = МножествоКарта.Пусто();

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура НеПусто() Экспорт

	// Дано

	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);

	МножествоКарта = Новый МножествоКарта;

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		Соответствия.КакКарта(Соответствие)
	);

	// Когда

	Результат = МножествоКарта.Пусто();

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура ПроцессорКоллекции() Экспорт
	
	// Дано

	МножествоКарта = Новый МножествоКарта;

	// Когда

	Результат = МножествоКарта.ПроцессорКоллекции();

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("ПроцессорКоллекций");

КонецПроцедуры

&Тест
Процедура Количество() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		Соответствия.КакКарта(Соответствие)
	);

	// Когда

	Результат = МножествоКарта.Количество();

	// Тогда
	
	Ожидаем.Что(Результат).Равно(3);

КонецПроцедуры

&Тест
Процедура Добавить() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = МножествоКарта.Добавить(4);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

	Ожидаем.Что(Соответствие).ИмеетДлину(4);
	Ожидаем.Что(Соответствие.Получить(1)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(2)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(3)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(4)).Не_().ЭтоНеопределено();

КонецПроцедуры

&Тест
Процедура НеДобавить() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = МножествоКарта.Добавить(2);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

	Ожидаем.Что(Соответствие).ИмеетДлину(3);
	Ожидаем.Что(Соответствие.Получить(1)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(2)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(3)).Не_().ЭтоНеопределено();

КонецПроцедуры

&Тест
Процедура ДобавитьВсе() Экспорт
	
	// Дано
	
	МножествоКарта = Новый МножествоКарта;

	// Когда

	Результат = МножествоКарта.ДобавитьВсе(
		МножествоФикстура()
	);

	// Тогда

	Ожидаем.Что(Результат).ЭтоИстина();

	Соответствие = Рефлектор.ПолучитьСвойство(
		Рефлектор.ПолучитьСвойство(МножествоКарта, "ВнутренняяКарта"), "Соответствие"
	);

	Ожидаем.Что(Соответствие).ИмеетДлину(3);
	Ожидаем.Что(Соответствие.Получить(1)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(2)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(3)).Не_().ЭтоНеопределено();

КонецПроцедуры

&Тест
Процедура НеДобавитьВсе() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = МножествоКарта.ДобавитьВсе(
		МножествоФикстура()
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();
	
	Ожидаем.Что(Соответствие).ИмеетДлину(3);
	Ожидаем.Что(Соответствие.Получить(1)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(2)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(3)).Не_().ЭтоНеопределено();

КонецПроцедуры

&Тест
Процедура Очистить() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	МножествоКарта.Очистить();

	// Тогда
	
	Ожидаем.Что(Соответствие).ИмеетДлину(0);

КонецПроцедуры

&Тест
Процедура Удалить() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = МножествоКарта.Удалить(2);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

	Ожидаем.Что(Соответствие).ИмеетДлину(2);
	Ожидаем.Что(Соответствие.Получить(1)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(2)).ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(3)).Не_().ЭтоНеопределено();

КонецПроцедуры

&Тест
Процедура НеУдалить() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = МножествоКарта.Удалить(4);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

	Ожидаем.Что(Соответствие).ИмеетДлину(3);
	Ожидаем.Что(Соответствие.Получить(1)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(2)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(3)).Не_().ЭтоНеопределено();

КонецПроцедуры

&Тест
Процедура УдалитьВсе() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(4, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = МножествоКарта.УдалитьВсе(МножествоФикстура());

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();
	
	Ожидаем.Что(Соответствие).ИмеетДлину(1);
	Ожидаем.Что(Соответствие.Получить(4)).Не_().ЭтоНеопределено();

КонецПроцедуры

&Тест
Процедура НеУдалитьВсе() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(4, Истина);
	Соответствие.Вставить(5, Истина);
	Соответствие.Вставить(6, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = МножествоКарта.УдалитьВсе(МножествоФикстура());

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

	Ожидаем.Что(Соответствие).ИмеетДлину(3);
	Ожидаем.Что(Соответствие.Получить(4)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(5)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(6)).Не_().ЭтоНеопределено();

КонецПроцедуры

&Тест
Процедура УдалитьЕсли() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = МножествоКарта.УдалитьЕсли(
		Новый Действие(ЭтотОбъект, "БольшеИлиРавноДвум")
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

	Ожидаем.Что(Соответствие).ИмеетДлину(1);
	Ожидаем.Что(Соответствие.Получить(1)).Не_().ЭтоНеопределено();

КонецПроцедуры

&Тест
Процедура НеУдалитьЕсли() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(0, Истина);
	Соответствие.Вставить(1, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = МножествоКарта.УдалитьЕсли(
		Новый Действие(ЭтотОбъект, "БольшеИлиРавноДвум")
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

	Ожидаем.Что(Соответствие).ИмеетДлину(2);
	Ожидаем.Что(Соответствие.Получить(0)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(1)).Не_().ЭтоНеопределено();

КонецПроцедуры

&Тест
Процедура УдалитьЕслиЛямбда() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(3, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = МножествоКарта.УдалитьЕсли(
		"Элемент -> Элемент >= 2"
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

	Ожидаем.Что(Соответствие).ИмеетДлину(1);
	Ожидаем.Что(Соответствие.Получить(1)).Не_().ЭтоНеопределено();

КонецПроцедуры

&Тест
Процедура СохранитьВсе() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(0, Истина);
	Соответствие.Вставить(1, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = МножествоКарта.СохранитьВсе(
		МножествоФикстура()
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

	Ожидаем.Что(Соответствие).ИмеетДлину(1);
	Ожидаем.Что(Соответствие.Получить(1)).Не_().ЭтоНеопределено();

КонецПроцедуры

&Тест
Процедура НеСохранитьВсе() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = МножествоКарта.СохранитьВсе(
		МножествоФикстура()
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

	Ожидаем.Что(Соответствие).ИмеетДлину(2);
	Ожидаем.Что(Соответствие.Получить(1)).Не_().ЭтоНеопределено();
	Ожидаем.Что(Соответствие.Получить(2)).Не_().ЭтоНеопределено();

КонецПроцедуры

&Тест
Процедура ВМассив() Экспорт
	
	// Дано
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = МножествоКарта.ВМассив();

	// Тогда
	
	Ожидаем.Что(Результат)
		.ИмеетТип("Массив")
		.ИмеетДлину(2)
		.Содержит(1)
		.Содержит(2);

КонецПроцедуры

&Тест
Процедура МутацияИнвалидируетИтераторы() Экспорт
	
	// Дано

	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);
	
	Итератор = МножествоКарта.Итератор();

	// Когда

	МножествоКарта.Добавить(4);

	// Тогда

	Ожидаем.Что(Итератор)
		.Метод("Следующий")
		.ВыбрасываетИсключение("Коллекция была изменена в процессе обхода");

КонецПроцедуры

&Тест
Процедура ПреставлениеПереопределяется() Экспорт
	
	// Дано

	МножествоКарта = Новый МножествоКарта;

	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);
	Соответствие.Вставить(МножествоКарта, Истина);
	Соответствие.Вставить(ТестМодуль.ОбъектЗаглушка(), Истина);

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	Результат = Строка(МножествоКарта);

	// Тогда
	
	Ожидаем.Что(Результат)
		.Равно("[1, 2, (Эта коллекция), Заглушка]");

КонецПроцедуры

&Тест
Процедура ОбходитсяЦикломДляКаждого() Экспорт

	Если Не ТестМодуль.ЭтоДвижокВерсии2() Тогда
		Возврат;
	КонецЕсли;

	// Дано

	Соответствие = Новый Соответствие;
	Соответствие.Вставить(1, Истина);
	Соответствие.Вставить(2, Истина);

	МножествоКарта = Новый МножествоКарта;

	ВнутренняяКарта = Соответствия.КакКарта(Соответствие);
	Соответствие = Рефлектор.ПолучитьСвойство(ВнутренняяКарта, "Соответствие");

	Рефлектор.УстановитьСвойство(
		МножествоКарта,
		"ВнутренняяКарта",
		ВнутренняяКарта
	);

	// Когда

	// Тогда

	Для каждого Элемент Из МножествоКарта Цикл

		Ожидаем.Что(Элемент)
			.БольшеИлиРавно(1)
			.МеньшеИлиРавно(2);

	КонецЦикла;

КонецПроцедуры

Функция БольшеИлиРавноДвум(Элемент) Экспорт
	Два = 2;
	Возврат Элемент >= Два;
КонецФункции

Процедура ДействиеДляКаждого(Элемент) Экспорт

	ДляКаждогоВыполнилсяРаз = ДляКаждогоВыполнилсяРаз + 1;

	Ожидаем.Что(Элемент).Равно(ДляКаждогоВыполнилсяРаз);

КонецПроцедуры

Функция МножествоФикстура()
	
	Если МножествоФикстура <> Неопределено Тогда
		Возврат МножествоФикстура;
	КонецЕсли;

	Коллекция = Новый Массив;
	Коллекция.Добавить(1);
	Коллекция.Добавить(2);
	Коллекция.Добавить(3);

	ПолеМассив = Новый Поле("Массив")
		.Публичное()
		.ЗначениеПоУмолчанию(Коллекция);

	МетодИтератор = Новый Метод("Итератор")
		.Публичный()
		.ТелоМетода("Возврат Новый ИтераторМассив(Массив, ЭтотОбъект);");

	МетодСодержит = Новый Метод("Содержит")
		.Публичный()
		.Параметр(Новый ПараметрМетода("Элемент"))
		.ТелоМетода("Возврат Массив.Найти(Элемент) <> Неопределено;");

	МножествоФикстура = Новый ПостроительДекоратора()
		.Поле(ПолеМассив)
		.Метод(МетодИтератор)
		.Метод(МетодСодержит)
		.Построить();

	Возврат МножествоФикстура;

КонецФункции

Рефлектор = Новый Рефлектор;
