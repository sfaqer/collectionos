#Использовать asserts

#Использовать ".."
#Использовать "./ТестМодуль"

Перем Рефлектор;               // Рефлектор
Перем ДляКаждогоВыполнилсяРаз; // Количество раз которое выполнился метод ДляКаждого
Перем ОчередьФикстура;         // Кеш очереди фикстуры

&Тест
Процедура ПриоритетнаяОчередьСоздается() Экспорт
	
	// Дано

	// Когда

	Результат = Новый ПриоритетнаяОчередь;

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("ПриоритетнаяОчередь");
	Ожидаем.Что(Рефлектор.ПолучитьСвойство(Результат, "Компаратор")).Равно(СравнениеЗначений.ПрямойПорядок());

КонецПроцедуры

&Тест
Процедура ПриоритетнаяОчередьСоздаетсяКомпараторДействие() Экспорт
	
	// Дано

	// Когда

	Компаратор = КомпараторДействие();

	Результат = Новый ПриоритетнаяОчередь(Компаратор);

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("ПриоритетнаяОчередь");
	Ожидаем.Что(Рефлектор.ПолучитьСвойство(Результат, "Компаратор")).Равно(Компаратор);

КонецПроцедуры

&Тест
Процедура ПриоритетнаяОчередьСоздаетсяКомпараторЛямбда() Экспорт
	
	// Дано

	// Когда

	Результат = Новый ПриоритетнаяОчередь("Первый, Второй -> {
		|	Если Первый % 2 > Второй % 2 Тогда
		|		Возврат 1;
		|	ИначеЕсли Первый % 2 = Второй % 2 Тогда
		|		Возврат 0;
		|	Иначе
		|		Возврат -1;
		|	КонецЕсли; 
		|}"
	);

	Компаратор = Рефлектор.ПолучитьСвойство(Результат, "Компаратор");

	// Тогда
	Ожидаем.Что(Результат).ИмеетТип("ПриоритетнаяОчередь");
	Ожидаем.Что(Компаратор.Выполнить(1, 2)).Равно(Компаратор(1, 2));
	Ожидаем.Что(Компаратор.Выполнить(2, 1)).Равно(Компаратор(2, 1));
	Ожидаем.Что(Компаратор.Выполнить(2, 2)).Равно(Компаратор(2, 2));

КонецПроцедуры

&Тест
Процедура ПриоритетнаяОчередьНеСоздаетсяБезКомпаратора() Экспорт
	
	// Дано

	БылоИсключение = Ложь;

	// Когда
	
	Попытка
		А = Новый ПриоритетнаяОчередь(1); // BSLLS:UnusedLocalVariable-off
	Исключение
		
		БылоИсключение = Истина;
		Ожидаем.Что(ИнформацияОбОшибке().Описание).Равно("Компаратором может выступать либо Действие либо лямбда-выражение");

	КонецПопытки;

	// Тогда

	Ожидаем.Что(БылоИсключение).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура Итератор() Экспорт

	// Дано
	
	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	// Когда

	Результат = ПриоритетнаяОчередь.Итератор();

	// Тогда
	
	Ожидаем.Что(Результат).ИмеетТип("ИтераторМассив");

КонецПроцедуры

&Тест
Процедура ДляКаждого() Экспорт

	// Дано

	ДляКаждогоВыполнилсяРаз = 0;

	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);
	Массив.Добавить(3);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь();

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	ПриоритетнаяОчередь.ДляКаждого(Новый Действие(ЭтотОбъект, "ДействиеДляКаждого"));

	// Тогда
	
	Ожидаем.Что(ДляКаждогоВыполнилсяРаз).Равно(3);

КонецПроцедуры

&Тест
Процедура ДляКаждогоЛямбда() Экспорт

	// Дано

	ТестМодуль.ДляКаждогоВыполнилсяРаз = 0;
	ТестМодуль.СуммаЭлементов          = 0;

	Массив = Новый Массив;
	Массив.Добавить(3);
	Массив.Добавить(2);
	Массив.Добавить(1);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	ПриоритетнаяОчередь.ДляКаждого(
		"Элемент -> ТестМодуль.ДляКаждогоВыполнилсяРаз = ТестМодуль.ДляКаждогоВыполнилсяРаз + 1;
		|	ТестМодуль.СуммаЭлементов = ТестМодуль.СуммаЭлементов + Элемент;"
	);

	// Тогда
	
	Ожидаем.Что(ТестМодуль.ДляКаждогоВыполнилсяРаз).Равно(3);
	Ожидаем.Что(ТестМодуль.СуммаЭлементов).Равно(6);

КонецПроцедуры

&Тест
Процедура Содержит() Экспорт

	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(3);
	Массив.Добавить(2);
	Массив.Добавить(1);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.Содержит(2);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура НеСодержит() Экспорт

	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(3);
	Массив.Добавить(2);
	Массив.Добавить(1);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.Содержит(4);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура СодержитВсе() Экспорт

	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(3);
	Массив.Добавить(2);
	Массив.Добавить(1);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.СодержитВсе(
		ОчередьФикстура()
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура СодержитНеВсе() Экспорт

	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(2);
	Массив.Добавить(1);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.СодержитВсе(
		ОчередьФикстура()
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура Пусто() Экспорт

	// Дано

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Новый Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.Пусто();

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

КонецПроцедуры

&Тест
Процедура НеПусто() Экспорт

	// Дано

	Массив = Новый Массив;
	Массив.Добавить(1);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);
	// Когда

	Результат = ПриоритетнаяОчередь.Пусто();

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

КонецПроцедуры

&Тест
Процедура ПроцессорКоллекции() Экспорт
	
	// Дано

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	// Когда

	Результат = ПриоритетнаяОчередь.ПроцессорКоллекции();

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("ПроцессорКоллекций");

КонецПроцедуры

&Тест
Процедура Количество() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(3);
	Массив.Добавить(2);
	Массив.Добавить(1);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.Количество();

	// Тогда
	
	Ожидаем.Что(Результат).Равно(3);

КонецПроцедуры

&Тест
Процедура Добавить() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(3);
	Массив.Добавить(2);
	Массив.Добавить(1);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.Добавить(4);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

	Ожидаем.Что(Массив).ИмеетДлину(4);
	Ожидаем.Что(Массив[0]).Равно(4);
	Ожидаем.Что(Массив[1]).Равно(3);
	Ожидаем.Что(Массив[2]).Равно(2);
	Ожидаем.Что(Массив[3]).Равно(1);

КонецПроцедуры

&Тест
Процедура ДобавитьКастомныйКомпаратор() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);
	Массив.Добавить(4);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь(КомпараторДействие());

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда
	Результат = ПриоритетнаяОчередь.Добавить(3);

	// Тогда
	Ожидаем.Что(Результат).ЭтоИстина();

	Ожидаем.Что(Массив).ИмеетДлину(4);
	Ожидаем.Что(Массив[0]).Равно(1);
	Ожидаем.Что(Массив[1]).Равно(3);
	Ожидаем.Что(Массив[2]).Равно(2);
	Ожидаем.Что(Массив[3]).Равно(4);

КонецПроцедуры

&Тест
Процедура ДобавитьНеопределено() Экспорт

	// Дано

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;
	Параметры = Новый Массив;
	Параметры.Добавить(Неопределено);

	// Когда

	// Тогда

	Ожидаем.Что(ПриоритетнаяОчередь).Метод("Добавить", Параметры)
		.ВыбрасываетИсключение("Очередь не может содержать Неопределено");

КонецПроцедуры

&Тест
Процедура ДобавитьВсе() Экспорт
	
	// Дано
	
	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	// Когда

	Результат = ПриоритетнаяОчередь.ДобавитьВсе(
		ОчередьФикстура()
	);

	// Тогда

	Ожидаем.Что(Результат).ЭтоИстина();

	Массив = Рефлектор.ПолучитьСвойство(ПриоритетнаяОчередь, "Массив");

	Ожидаем.Что(Массив).ИмеетДлину(3);
	Ожидаем.Что(Массив[0]).Равно(3);
	Ожидаем.Что(Массив[1]).Равно(2);
	Ожидаем.Что(Массив[2]).Равно(1);

КонецПроцедуры

&Тест
Процедура НеДобавитьВсе() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);
	Массив.Добавить(3);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.ДобавитьВсе(
		ПустаяОчередьФикстура()
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();
	
	Ожидаем.Что(Массив).ИмеетДлину(3);
	Ожидаем.Что(Массив[0]).Равно(1);
	Ожидаем.Что(Массив[1]).Равно(2);
	Ожидаем.Что(Массив[2]).Равно(3);

КонецПроцедуры

&Тест
Процедура Очистить() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);
	Массив.Добавить(3);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	ПриоритетнаяОчередь.Очистить();

	// Тогда
	
	Ожидаем.Что(Массив).ИмеетДлину(0);

КонецПроцедуры

&Тест
Процедура Удалить() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);
	Массив.Добавить(3);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.Удалить(2);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

	Ожидаем.Что(Массив).ИмеетДлину(2);
	Ожидаем.Что(Массив[0]).Равно(1);
	Ожидаем.Что(Массив[1]).Равно(3);

КонецПроцедуры

&Тест
Процедура НеУдалить() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);
	Массив.Добавить(3);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.Удалить(4);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

	Ожидаем.Что(Массив).ИмеетДлину(3);
	Ожидаем.Что(Массив[0]).Равно(1);
	Ожидаем.Что(Массив[1]).Равно(2);
	Ожидаем.Что(Массив[2]).Равно(3);

КонецПроцедуры

&Тест
Процедура УдалитьВсе() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);
	Массив.Добавить(4);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.УдалитьВсе(ОчередьФикстура());

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();
	
	Ожидаем.Что(Массив).ИмеетДлину(1);
	Ожидаем.Что(Массив[0]).Равно(4);

КонецПроцедуры

&Тест
Процедура НеУдалитьВсе() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(4);
	Массив.Добавить(5);
	Массив.Добавить(6);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.УдалитьВсе(ОчередьФикстура());

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

	Ожидаем.Что(Массив).ИмеетДлину(3);
	Ожидаем.Что(Массив[0]).Равно(4);
	Ожидаем.Что(Массив[1]).Равно(5);
	Ожидаем.Что(Массив[2]).Равно(6);

КонецПроцедуры

&Тест
Процедура УдалитьЕсли() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);
	Массив.Добавить(3);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.УдалитьЕсли(
		Новый Действие(ЭтотОбъект, "БольшеИлиРавноДвум")
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

	Ожидаем.Что(Массив).ИмеетДлину(1);
	Ожидаем.Что(Массив[0]).Равно(1);

КонецПроцедуры

&Тест
Процедура НеУдалитьЕсли() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(0);
	Массив.Добавить(1);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.УдалитьЕсли(
		Новый Действие(ЭтотОбъект, "БольшеИлиРавноДвум")
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

	Ожидаем.Что(Массив).ИмеетДлину(2);
	Ожидаем.Что(Массив[0]).Равно(0);
	Ожидаем.Что(Массив[1]).Равно(1);

КонецПроцедуры

&Тест
Процедура УдалитьЕслиЛямбда() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);
	Массив.Добавить(3);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);
	// Когда

	Результат = ПриоритетнаяОчередь.УдалитьЕсли(
		"Элемент -> Элемент >= 2"
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

	Ожидаем.Что(Массив).ИмеетДлину(1);
	Ожидаем.Что(Массив[0]).Равно(1);

КонецПроцедуры

&Тест
Процедура СохранитьВсе() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(0);
	Массив.Добавить(1);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.СохранитьВсе(
		ОчередьФикстура()
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

	Ожидаем.Что(Массив).ИмеетДлину(1);
	Ожидаем.Что(Массив[0]).Равно(1);

КонецПроцедуры

&Тест
Процедура НеСохранитьВсе() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.СохранитьВсе(
		ОчередьФикстура()
	);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоЛожь();

	Ожидаем.Что(Массив).ИмеетДлину(2);
	Ожидаем.Что(Массив[0]).Равно(1);
	Ожидаем.Что(Массив[1]).Равно(2);

КонецПроцедуры

&Тест
Процедура ВМассив() Экспорт
	
	// Дано

	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.ВМассив();

	// Тогда
	
	Ожидаем.Что(Результат)
		.ИмеетТип("Массив")
		.ИмеетДлину(2)
		.Содержит(1)
		.Содержит(2)
		.Не_().Равно(Массив);

КонецПроцедуры

&Тест
Процедура МутацияИнвалидируетИтераторы() Экспорт
	
	// Дано

	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);
	
	Итератор = ПриоритетнаяОчередь.Итератор();

	// Когда

	ПриоритетнаяОчередь.Добавить(4);

	// Тогда

	Ожидаем.Что(Итератор)
		.Метод("Следующий")
		.ВыбрасываетИсключение("Коллекция была изменена в процессе обхода");

КонецПроцедуры

&Тест
Процедура ПреставлениеПереопределяется() Экспорт
	
	// Дано

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);
	Массив.Добавить(ПриоритетнаяОчередь);
	Массив.Добавить(ТестМодуль.ОбъектЗаглушка());

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = Строка(ПриоритетнаяОчередь);

	// Тогда
	
	Ожидаем.Что(Результат)
		.Равно("[1, 2, (Эта коллекция), Заглушка]");

КонецПроцедуры

&Тест
Процедура ОбходитсяЦикломДляКаждого() Экспорт

	Если Не ТестМодуль.ЭтоДвижокВерсии2() Тогда
		Возврат;
	КонецЕсли;

	// Дано

	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;
	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	// Тогда

	Для каждого Элемент Из ПриоритетнаяОчередь Цикл

		Ожидаем.Что(Элемент)
			.БольшеИлиРавно(1)
			.МеньшеИлиРавно(2);

	КонецЦикла;

КонецПроцедуры

&Тест
Процедура ПодсмотретьОчередьПуста() Экспорт

	// Дано

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	// Когда

	Результат = ПриоритетнаяОчередь.Подсмотреть();

	// Тогда

	Ожидаем.Что(Результат)
		.Равно(Опциональные.Пустой());

КонецПроцедуры

&Тест
Процедура ПодсмотретьОчередьНеПуста() Экспорт

	// Дано

	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);
	Массив.Добавить(3);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.Подсмотреть();

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("Опциональный");
	Ожидаем.Что(Результат.Пустой()).ЭтоЛожь();
	Ожидаем.Что(Результат.Получить()).Равно(1);

	Ожидаем.Что(Массив).ИмеетДлину(3);
	Ожидаем.Что(Массив[0]).Равно(1);
	Ожидаем.Что(Массив[1]).Равно(2);
	Ожидаем.Что(Массив[2]).Равно(3);

КонецПроцедуры

&Тест
Процедура Положить() Экспорт
	
	// Дано
	
	Массив = Новый Массив;
	Массив.Добавить(3);
	Массив.Добавить(2);
	Массив.Добавить(1);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.Положить(4);

	// Тогда
	
	Ожидаем.Что(Результат).ЭтоИстина();

	Ожидаем.Что(Массив).ИмеетДлину(4);
	Ожидаем.Что(Массив[0]).Равно(4);
	Ожидаем.Что(Массив[1]).Равно(3);
	Ожидаем.Что(Массив[2]).Равно(2);
	Ожидаем.Что(Массив[3]).Равно(1);

КонецПроцедуры

&Тест
Процедура ВзятьОчередьПуста() Экспорт

	// Дано

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь;

	// Когда

	Результат = ПриоритетнаяОчередь.Взять();

	// Тогда

	Ожидаем.Что(Результат)
		.Равно(Опциональные.Пустой());

КонецПроцедуры

&Тест
Процедура ВзятьОчередьНеПуста() Экспорт

	// Дано

	Массив = Новый Массив;
	Массив.Добавить(1);
	Массив.Добавить(2);
	Массив.Добавить(3);

	ПриоритетнаяОчередь = Новый ПриоритетнаяОчередь(СравнениеЗначений.ОбратныйПорядок());

	Рефлектор.УстановитьСвойство(
		ПриоритетнаяОчередь,
		"Массив",
		Массив
	);

	// Когда

	Результат = ПриоритетнаяОчередь.Взять();

	// Тогда

	Ожидаем.Что(Результат).ИмеетТип("Опциональный");
	Ожидаем.Что(Результат.Пустой()).ЭтоЛожь();
	Ожидаем.Что(Результат.Получить()).Равно(1);

	Ожидаем.Что(Массив).ИмеетДлину(2);
	Ожидаем.Что(Массив[0]).Равно(2);
	Ожидаем.Что(Массив[1]).Равно(3);

КонецПроцедуры

Функция БольшеИлиРавноДвум(Элемент) Экспорт
	Два = 2;
	Возврат Элемент >= Два;
КонецФункции

Процедура ДействиеДляКаждого(Элемент) Экспорт

	ДляКаждогоВыполнилсяРаз = ДляКаждогоВыполнилсяРаз + 1;

	Ожидаем.Что(Элемент).Равно(ДляКаждогоВыполнилсяРаз);

КонецПроцедуры

Функция Компаратор(Первый, Второй) Экспорт
	
	Если Первый % 2 > Второй % 2 Тогда
		Возврат 1;
	ИначеЕсли Первый % 2 = Второй % 2 Тогда
		Возврат 0;
	Иначе
		Возврат -1;
	КонецЕсли; 

КонецФункции

Функция КомпараторДействие()
	Возврат Новый Действие(ЭтотОбъект, "Компаратор");
КонецФункции

Функция ОчередьФикстура()
	
	Если ОчередьФикстура <> Неопределено Тогда
		Возврат ОчередьФикстура;
	КонецЕсли;

	Коллекция = Новый Массив;
	Коллекция.Добавить(1);
	Коллекция.Добавить(2);
	Коллекция.Добавить(3);

	ПолеМассив = Новый Поле("Массив")
		.Публичное()
		.ЗначениеПоУмолчанию(Коллекция);

	МетодИтератор = Новый Метод("Итератор")
		.Публичный()
		.ТелоМетода("Возврат Новый ИтераторМассив(Массив, ЭтотОбъект);");

	МетодСодержит = Новый Метод("Содержит")
		.Публичный()
		.Параметр(Новый ПараметрМетода("Элемент"))
		.ТелоМетода("Возврат Массив.Найти(Элемент) <> Неопределено;");

	ОчередьФикстура = Новый ПостроительДекоратора()
		.Поле(ПолеМассив)
		.Метод(МетодИтератор)
		.Метод(МетодСодержит)
		.Построить();

	Возврат ОчередьФикстура;

КонецФункции

Функция ПустаяОчередьФикстура()
	
	МетодИтератор = Новый Метод("Итератор")
		.Публичный()
		.ТелоМетода("Возврат Новый ИтераторМассив(Новый Массив, ЭтотОбъект);");

	Возврат Новый ПостроительДекоратора()
		.Метод(МетодИтератор)
		.Построить();

КонецФункции

Рефлектор = Новый Рефлектор;
